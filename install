#!/usr/bin/env fish

if set -q XDG_CONFIG_HOME && test -n $XDG_CONFIG_HOME
	set config_home $XDG_CONFIG_HOME
else
	set config_home ~/.config
end

set here (dirname (realpath (status filename)))
set dotfiles $here/dotfiles

if not contains $here/bin $fish_user_paths
	set --prepend --universal fish_user_paths $here/bin
end

ln -sf $dotfiles/config.fish $config_home/fish/
rm -r $config_home/fish/functions
ln -sf $dotfiles/functions $config_home/fish/

# legacy path
unlink ~/.nanorc 2>/dev/null || true

mkdir -p $config_home/nano
ln -sf $dotfiles/nanorc $config_home/nano/nanorc
ln -sf $dotfiles/ghci ~/.ghci

for py_version in 3.7 3.8 3.9 3.10 3.11 3.12
	if type -q python$py_version
		mkdir -p ~/.local/lib/python$py_version/site-packages/
		ln -sf $dotfiles/sitecustomize.py ~/.local/lib/python$py_version/site-packages/
	end
end

ln -sf $dotfiles/discord-custom.css $config_home/discord/discord-custom.css
command -q beautifuldiscord; and beautifuldiscord --css $config_home/discord/discord-custom.css >/dev/null 2>&1

# we were previously using ~/.gitignore, now let's use the default
git config --global --unset core.excludesFile

mkdir -p $config_home/git/
ln -sf $dotfiles/gitignore $config_home/git/ignore

# X11 stuff
# this expects that your ~/.xinitrc will source every file in $config_home/xinit/xinitrc.d
# TODO get XCompose to use a custom path
ln -sf $dotfiles/x/xcompose ~/.XCompose
if not test -d $config_home/xinit
	ln -sf $dotfiles/x $config_home/xinit
end
